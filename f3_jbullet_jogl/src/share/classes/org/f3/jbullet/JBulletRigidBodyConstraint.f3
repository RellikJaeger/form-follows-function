package org.f3.jbullet;

function setImpl of (a is f3.media.scene.RigidBodyConstraint) 
    from (x is JBulletRigidBodyConstraint of a, y is a) to ()
{
    y.impl = x;
}

public abstract class JBulletRigidBodyConstraint of (a is f3.media.scene.RigidBodyConstraint) {

    public var constraint is a on replace { // hack
        setImpl(this, constraint);
    }
    
    protected var tc is com.bulletphysics.dynamics.constraintsolver.TypedConstraint;

    public function getConstraint from (x is com.bulletphysics.dynamics.RigidBody,
                                        y is com.bulletphysics.dynamics.RigidBody)
        to com.bulletphysics.dynamics.constraintsolver.TypedConstraint 
    {
        if (tc == null) {
            tc = createConstraint(x, y);
            tc.setUserData(new java.lang.ref.WeakReference of JBulletRigidBodyConstraint(this));
        }
        return tc;
    }

    protected abstract function createConstraint
        from (x is com.bulletphysics.dynamics.RigidBody,
              y is com.bulletphysics.dynamics.RigidBody) 
        to
        com.bulletphysics.dynamics.constraintsolver.TypedConstraint;

}
