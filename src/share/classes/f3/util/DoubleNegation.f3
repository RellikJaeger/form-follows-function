package f3.util;

//
// Continuation Monad/Comonad - corresponds to the logical double negation of 'a'
//
// The logical negation of 'a' can be represented in terms of implication:
//
//    a implies false
//
// and hence double negation:
//
//    (a implies false) implies false
//
// and taking implication as function abstraction:
//  
//    function from (function from a to false) to false
//
// However, rather than representing just a truth value, "false" is abstracted as an 
// existentially quantified type variable, thus:
//
//    function of b from (function from a to b) to b
// 

public const monad = Monad of DoubleNegation {
    override function map of (a, b) 
	from (xs is DoubleNegation of a,
	      f is function from a to b) 
	to DoubleNegation of b 
    {
	xs.map(f)
    }

    override function flatmap of (a, b) 
	from (xs is DoubleNegation of a,
	      f is function from a to DoubleNegation of b) to DoubleNegation of b 
    {
	xs.flatmap(f)
    }

    override function return of a from (x is a) to DoubleNegation of a 
    {
	doublyNegate(x)
    }
}

public abstract class DoubleNegation of a 
       is org.f3.runtime.Monad of (DoubleNegation, a), org.f3.runtime.Comonad of (DoubleNegation, a) 
{
    public abstract function negate of _ 
	from (negate_a is function from a to _) 
	to _;

    public override function map of b 
	from (f is function from a to b) 
	to DoubleNegation of b 
    {
	const self = this;
	DoubleNegation of b
	{
	    override function negate of _ from (negate_b is function from b to _) to _ 
	    {
		self.negate(negate_b * f)
	    }
	}
    }

    public function flatmap of b 
	from (f is function from a to DoubleNegation of b) 
	to DoubleNegation of b 
    {
    	const self = this;
	DoubleNegation of b 
	{
	    override function negate of _ 
		from (negate_b is function from b to _) 
                to _ 
	    {
		const negate_a =
		    function from (x is a) to _ 
		    {
			const m is DoubleNegation of b = f(x);
			m.negate(negate_b);
		    }
		self.negate(negate_a);
	    }
	}
    }

    public function coflatmap of b 
	from (f is function from DoubleNegation of a to b) 
	to DoubleNegation of b 
    {
    	const self = this;
	DoubleNegation of b
	{
	    override function negate of _ from (negate_b is function from b to _) to _
		{
		    const x is b = f(self);
		    negate_b(x);
		}
	}
    }

    public function extract to a from ()
    { 
	doublyNegate();
    }

    public function doublyNegate to a from ()
    {
	this.negate(id of a);
    }

    override function toString to String from ()
    {
	"(not not {doublyNegate()})"
    }

    override function equals from (obj is Object) to Boolean 
    {
	if (obj is DoubleNegation) {
	    (obj as DoubleNegation of Object).doublyNegate() == doublyNegate();
	} else false
    }
}

/*
public function doublyNegate of (a, b)
    from (f is function from a to b) 
    to function from DoubleNegation of a to DoubleNegation of b
{
    function from (xs is DoubleNegation of a) to DoubleNegation of b 
    {
	foreach (x in xs) f(x)
    }
}
*/

// this is the unit of the monad
public function doublyNegate of a 
    from (x is a) 
    to DoubleNegation of a 
{
    DoubleNegation of a 
    {
	override function negate of _ from (neg is function from a to _) to _ 
	{
	    neg(x)
	}
    }
}

public function counit of a 
    from (x is DoubleNegation of a) 
    to a 
{
    x.doublyNegate() 
}

public function unit of a 
    from (x is a) 
    to DoubleNegation of a 
{ 
    doublyNegate(x) 
}

public function join of a 
    from (x is DoubleNegation of DoubleNegation of a) 
    to DoubleNegation of a 
{ 
    // x.doublyNegate();
    DoubleNegation of a {
	override function negate of _ from (negate_a is function from a to _) to _ 
	{
	    x.negate(function from (y is DoubleNegation of a) to _ 
		     {
			 y.negate(negate_a);
		     })
	}
    }
}

public function cojoin of a
    from (x is DoubleNegation of a) 
    to DoubleNegation of DoubleNegation of a 
{ 
    DoubleNegation of DoubleNegation of a 
    {
	override function negate of _ 
	    from (neg_neg_a is function from DoubleNegation of a to _) 
	    to _ 
	{
	    neg_neg_a(x)
	}
    }
}

public function callCC of (a, _1)
    from (f is function /* of _1 */ from (function from a to _1) to _1) 
    to DoubleNegation of a 
{
    DoubleNegation of a 
    {
	override function negate of _2 
	    from (negate_a is function from a to _2) 
	    to _2
	{
	    f(negate_a)
	}
    }
}

public function run from (args is String[]) to ()
{ 
    println((foreach (x in doublyNegate(2), 
                      y in doublyNegate(3),
                      z in callCC(function from (k is function from Integer to Integer) to Integer                                  {
                                      if (args <> []) then Integer.parseInt(args[0])+x+y else k(x+y);
                                  })
                      ) {
                 x + y - z
             }).extract());       
}

