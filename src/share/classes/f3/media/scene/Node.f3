package f3.media.scene;
import f3.math.*;
import org.f3.runtime.*;

// A Node in the 3d spatial affine transform hierarchy

public abstract class Node is Foldable of Node
{
    override function toString to String {
	"{super.toString()} {id}"
    }
    public var id is String;
    public var cursor is Cursor;
    public readonly var parent is Node;
    public var visible is Boolean = true;
    public readonly var sceneVisible is Boolean =  
	bind visible and parent.sceneVisible;

    public readonly var parentSceneTransform is Mat4 = 
	bind if parent == null then Mat4.<<1>> else parent.sceneTransform;
    public readonly var transform is Mat4 = Mat4.<<1>>;
    public readonly var sceneTransform is Mat4 = 
	bind parentSceneTransform * transform;

    public readonly var bounds is Bounds = bind transform * contentBounds;
    public readonly var sceneBounds is Bounds = bind parentSceneTransform * bounds;
    public readonly var contentBounds is Bounds;

    public readonly var blocksPointer is Boolean = false;
    public readonly var containsPointer is Boolean;
    protected function setContainsPointer from (value is Boolean) to () { containsPointer = value; }
    public readonly var pickable is Boolean = true;
    public readonly var scenePickable is Boolean = 
	bind pickable and parent.scenePickable;

    override public function foldLeft of b
	from (z is b, f is function from (b, Node) to b) 
	to b
    {
	f(z, this);
    }

    override public function foldRight of b
	from (z is b, f is function from (Node, b) to b) 
	to b
    {
	f(this, z)
    }

    public function updateParent from () to () { update() }
    protected function setParent from (n is Node) to () { parent = n }
    public function update from () to () {}
}



