package f3.media.scene;
import f3.media.input.*;
import f3.math.*;

function addQuad 
    from (start is Integer,
          ib is IntBufferBuilder,
          vb is FloatBufferBuilder, 
          nb is FloatBufferBuilder, 
          tb is FloatBufferBuilder, 
          x is Number,
          y is Number,
          width is Number, 
          height is Number, 
          uw is Number, 
          uh is Number) to Integer
{
    const indices = [0, 1, 2, 0, 2, 3];
    for (i in indices) ib.put(start + i);
    vb.put(x-width).put(y+height).put(0);
    vb.put(x-width).put(y-height).put(0);
    vb.put(x+width).put(y-height).put(0);
    vb.put(x+width).put(y+height).put(0);
    for (i in [0..<4]) {
        nb.put(0).put(0).put(1);
    }
    const sx = width/uw;
    const sy = height/uh;
    const tx = (1.0-sx);
    const ty = (1.0-sy);
    tb.put(tx).put(1.0-ty);
    tb.put(tx).put(ty);
    tb.put(1.0-tx).put(ty);
    tb.put(1.0-tx).put(1.0-ty);
    return start + 4;
}

public class Billboard2D is AbstractNode, MouseInputHandler {
    const indexBuilder is IntBufferBuilder = new IntBufferBuilder();
    const pos is FloatBufferBuilder = new FloatBufferBuilder();
    const norm is FloatBufferBuilder = new FloatBufferBuilder();
    const uv is FloatBufferBuilder = new FloatBufferBuilder();
    var buffer is java.nio.FloatBuffer;
    var uvBuffer is java.nio.FloatBuffer;
    var normBuffer is java.nio.FloatBuffer;
    var indexBuffer is java.nio.IntBuffer;
    public var content is Image[];
    var imageBounds is Bounds[];

    var pressed is Pick;
    var over is Pick;

    function findTarget from (pt is Point3) to Pick
    {
        for (im in content where im.visible) {
            if (im.bounds.contains(pt)) {
                const p = Pick {
                    node: im;
                    t: 1;
                    dragPoint1: Point3.<<0>> + (im.bounds.center - pt);
                }
                return p;
            }
        }
        return null;
    }

    function retarget from (p is Pick, e is MouseEvent) to MouseEvent
    {
        MouseEvent {
            picked: p;
            count: e.count;
            clickCount: e.clickCount;
            stage: e.stage;
            scene: e.scene;
            altKey: e.altKey;
            ctrlKey: e.ctrlKey;
            shiftKey: e.shiftKey;
            metaKey: e.metaKey;
            screenx: e.screenx;
            screeny: e.screeny;
            screenDrag: e.screenDrag;
            sceneLocation: e.sceneLocation;
            location: p.dragPoint1;
            buttons: e.buttons;
        }
    }

    function getMouseInputHandlers from (p is Pick) to MouseInputHandler[] 
    {
        if (p.node is MouseInputHandler) then p.node as MouseInputHandler else null;
    }

    override public function onMouseUp from (event is MouseEvent) to () {
        for (h in getMouseInputHandlers(pressed)) {
            h.onMouseUp(event)
        }
        pressed = null;
    }

    override public function onMouseDown from (event is MouseEvent) to () {
        pressed = findTarget(event.location);
        for (h in getMouseInputHandlers(pressed)) {
            h.onMouseDown(retarget(pressed, event));
        }
    }

    override public function onMouseMove from (event is MouseEvent) to () {
        for (h in getMouseInputHandlers(over)) {
            h.onMouseMove(retarget(over, event));
        }
    }

    override public function onMouseDrag from (event is MouseEvent) to () {
        pressed.dragPoint0 = pressed.dragPoint1;
        pressed.dragPoint1 = Point3.<<0>> + (event.location - pressed.node.bounds.center);
        for (h in getMouseInputHandlers(pressed)) {
            h.onMouseDrag(retarget(pressed, event));
        }
    }

    override public function onMouseEnter from (event is MouseEvent) to () {
        over = findTarget(event.location);
        for (h in getMouseInputHandlers(over)) {
            h.onMouseEnter(retarget(over, event));
        }
    }

    override public function onMouseLeave from (event is MouseEvent) to () {
        for (h in getMouseInputHandlers(over)) {
            h.onMouseLeave(retarget(over, event));
        }
        over = null;
    }

    override public function onMouseWheel from (event is MouseWheelEvent) to () {
        //over.onMouseWheel(retarget(over, event))
    }

    override function updateParent to () {
        foreach (im in content) {
            im.setParent(this);
            im.updateParent;
        }
        super.updateParent();
    }

    const paint = Shape2D.TexturePaint {
        texture: bind content[0];
    }

    override function update to ()
    {
        imageBounds = foreach (im in content) im.bounds;
        super.update();
    }

    override var internalContent = MeshNode {
        shader: bind effectLoader.loadPaintShader(paint);
        override var mesh = bind getMesh(imageBounds);
    }
    
    public function getMesh from (bounds is Bounds[]) to AbstractMesh
    {
        indexBuilder.reset();
        pos.reset();
        norm.reset();
        uv.reset();
        var i = 0;
        for (x in bounds where x.extent.x > 0 and x.extent.y > 0) {
            const c = x.center;
            const w = x.extent.x;
            const h = x.extent.y;
            i = addQuad(i, indexBuilder, pos, null, uv, c.x, c.y, w, h, w, h);
        }
        println("CREATING BILLBOARD MESH: {i}");
        indexBuffer = indexBuilder.getBuffer(indexBuffer);
        buffer = pos.getBuffer(buffer);
        normBuffer = norm.getBuffer(normBuffer);
        uvBuffer = uv.getBuffer(uvBuffer);
	const vertexAttrs = MeshBuffer.VertexAttributeBuffer {
            id: "POSITION";
	    coordsPerVertex: 3;
	    buffer: buffer;
	}
	const normAttrs = MeshBuffer.VertexAttributeBuffer {
            id: "NORMAL";
	    coordsPerVertex: 3;
	    buffer: normBuffer;
	}
	const uvAttrs = MeshBuffer.VertexAttributeBuffer {
            id: "TEXCOORD0";
	    coordsPerVertex: 2;
	    buffer: uvBuffer;
	}
	MeshBuffer {
	    indexBuffer: indexBuffer;
	    vertexBuffer: vertexAttrs;
            normalBuffer: normAttrs;
	    textureBuffers: [uvAttrs];
	}.toMesh();
    }
}