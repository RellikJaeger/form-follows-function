/*
 * Copyright 2009, Google Inc.
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are
 * met:
 *
 *     * Redistributions of source code must retain the above copyright
 * notice, this list of conditions and the following disclaimer.
 *     * Redistributions in binary form must reproduce the above
 * copyright notice, this list of conditions and the following disclaimer
 * in the documentation and/or other materials provided with the
 * distribution.
 *     * Neither the name of Google Inc. nor the names of its
 * contributors may be used to endorse or promote products derived from
 * this software without specific prior written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
 * "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
 * LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
 * A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT
 * OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
 * SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT
 * LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
 * DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
 * THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
 * (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
 * OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */

uniform float4x4 worldViewProjection : WORLDVIEWPROJECTION;
uniform float4x4 world : WORLD;
uniform float4x4 viewInverse : VIEWINVERSE;
uniform float4x4 worldInverseTranspose : WORLDINVERSETRANSPOSE;
uniform float3 lightWorldPos1;
uniform float3 lightWorldPos2;
uniform float3 lightWorldPos3;
uniform float3 lightWorldPos4;
uniform float4 lightMask;
uniform float4 ambientIntensity;
uniform float4 lightIntensity;
uniform float4 emissive;
uniform float4 ambient;
uniform float4 colorMult;
uniform float4 diffuse;
uniform float4 specular;
uniform float shininess;
sampler texSampler0;  

struct VertexShaderInput {
  float4 position : POSITION;
  float4 normal : NORMAL;
  float2 tex : TEXCOORD0;  
};

struct PixelShaderInput {
  float4 position : POSITION;
  float3 normal : TEXCOORD1;
  float3 worldPosition : TEXCOORD4;
  float2 tex : TEXCOORD0;  
};

PixelShaderInput vertexShaderFunction(VertexShaderInput input) {
  PixelShaderInput output;
  output.position = mul(worldViewProjection, input.position);
  float3 worldPosition = mul(world, input.position).xyz;
  output.normal = mul(worldInverseTranspose, input.normal).xyz;
  output.worldPosition = worldPosition;
  output.tex = input.tex;  
  return output;
}

float4 doLight(float3 worldPosition, float3 lightWorldPos, float3 worldNormal, float3 surfaceToView)  {
  float3 surfaceToLight = normalize(lightWorldPos - worldPosition);
  float3 halfVector = normalize(surfaceToLight + surfaceToView);
  float4 litResult = lit(dot(worldNormal, surfaceToLight),
                         dot(worldNormal, halfVector), shininess);
  return litResult;
}

float4 pixelShaderFunction(PixelShaderInput input) : COLOR {
  float3 worldNormal = normalize(input.normal);
  float3 surfaceToView = normalize(float3(viewInverse[0].w,
                                          viewInverse[1].w,
                                          viewInverse[2].w) -
                                   input.worldPosition);
  float4 litResult1 = doLight(input.worldPosition, lightWorldPos1, worldNormal, surfaceToView) * lightMask.r;
  float4 litResult2 = doLight(input.worldPosition, lightWorldPos2, worldNormal, surfaceToView) * lightMask.g;
  float4 litResult3 = doLight(input.worldPosition, lightWorldPos3, worldNormal, surfaceToView) * lightMask.b;
  float4 litResult4 = doLight(input.worldPosition, lightWorldPos4, worldNormal, surfaceToView) * lightMask.a;
  float4 textureCooef = tex2D(texSampler0, input.tex);  
  float4 outColor = ambientIntensity * ambient * colorMult * textureCooef;
  outColor += lightIntensity * (diffuse * textureCooef * colorMult * (litResult1.y+litResult2.y+litResult3.y+litResult4.y) +
                                specular * (litResult1.z+litResult2.z+litResult3.z+litResult4.z));
  outColor += emissive;
  return float4(outColor.rgb, diffuse.a * colorMult.a);
}

technique Opaque {
    pass P0 {
	DepthTestEnable = true;
        DepthFunc = LessEqual;
        DepthMask = true;
        CullFaceEnable  = false; 
        BlendEnable = false;
        VertexShader = compile latest vertexShaderFunction();
        PixelShader  = compile latest pixelShaderFunction();
    }
}

technique Transparent {
    pass P1 {
	DepthTestEnable = true;
        DepthMask = false;
        BlendEnable = true;
        BlendFunc = int2(SrcAlpha, OneMinusSrcAlpha);
        VertexShader = compile latest vertexShaderFunction();
        PixelShader  = compile latest pixelShaderFunction();
    }
}
