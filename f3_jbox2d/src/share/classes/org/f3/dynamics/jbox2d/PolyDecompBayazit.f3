package org.f3.dynamics.jbox2d;
import f3.math.*;
import java.lang.Math;

public function area from (a is Point2, b is Point2, c is Point2) to Number {
    return (((b.x - a.x)*(c.y - a.y))-((c.x - a.x)*(b.y - a.y)));
}

public function right from (a is Point2, b is Point2, c is Point2) to Boolean {
    return area(a, b, c) < 0;
}

public function rightOn from (a is Point2, b is Point2, c is Point2) to Boolean {
    return area(a, b, c) <= 0;
}

public function left from (a is Point2, b is Point2, c is Point2) to Boolean {
    return area(a, b, c) > 0;
}

public function leftOn from (a is Point2, b is Point2, c is Point2) to Boolean {
    return area(a, b, c) >= 0;
}

public function sqdist from (a is Point2, b is Point2) to Number {
    var dx is Number = b.x - a.x;
    var dy is Number = b.y - a.y;
    return dx * dx + dy * dy;
}

public function getIntersection from (start1 is Point2, end1 is Point2, start2 is Point2, end2 is Point2) to Point2 {
    var a1 is Number = end1.y - start1.y;
    var b1 is Number = start1.x - end1.x;
    var c1 is Number = a1 * start1.x + b1 * start1.y;
    var a2 is Number = end2.y - start2.y;
    var b2 is Number = start2.x - end2.x;
    var c2 is Number = a2 * start2.x + b2 * start2.y;
    var det is Number = a1 * b2 - a2*b1;
    
    if (Math.abs(det) > LinearMath.ZERO_TOLERANCE) { // lines are not parallel
        return new Point2((b2 * c1 - b1 * c2) / det,  (a1 * c2 - a2 * c1) / det);
    }
    return null;
}

public class PolyDecompBayazit 
{     

    public function combineColinearPoint2s from () to () {
        // combine similar points
        var combinedPoint2s is Point2[] = [];

        for (i in [0..<points.size()]) {
            var a is Point2 = this.at(i - 1);
            var b is Point2 = this.at(i);
            var c is Point2 = this.at(i + 1);

            if (getIntersection(a, b, b, c) != null) {
                insert b into combinedPoint2s;
            }
        }

        points = combinedPoint2s;
    }

    public var points is Point2[];

    postinit {
        apply();
    }

    public function apply to () {
        combineClosePoint2s();
        combineColinearPoint2s();
        makeCCW();
    }

    public function combineClosePoint2s from () to () {
        var combinedPoint2s is Point2[] = [];
        for (i in [0..<points.size()]) {
            var a is Point2 = this.at(i);
            var b is Point2 = this.at(i + 1);

            if (sqdist(a, b) > LinearMath.ZERO_TOLERANCE) {
                insert a into combinedPoint2s;
            }
        }
        points = combinedPoint2s;
    }

    public function at from (i is Integer) to Point2 {
        var s is Integer = points.size();
        return points[(i + s) mod s];
    }

    public function isReflex from (i is Integer) to Boolean {
        return right(this.at(i - 1), this.at(i), this.at(i + 1));
    }

    public function polyFromRange from (lower is Integer, upper is Integer) to PolyDecompBayazit {
        if (lower < upper) {
            return PolyDecompBayazit { points: points[lower..upper] }
        } else {
            return PolyDecompBayazit { points: [points[lower..], [points[0..upper]]] }
        }
    }

    public function decompose from (cb is function from PolyDecompBayazit to ()) to () {
        if (points.size() < 3) return;

        for (i in [0..<points.size()]) {
            if (isReflex(i)) {
                // Find closest two vertices in range from a reflex point (two the vertices are by going CW and CCW around polygon)
                // See first diagram on this page is  http is //mnbayazit.com/406/bayazit
                var upperDist is Number = Number.MAX_VALUE;
                var upperIntersection is Point2 = null;
                var upperIndex is Integer = 0;
                var lowerDist is Number = Number.MAX_VALUE;
                var lowerIntersection is Point2 = null;
                var lowerIndex is Integer = 0;

                for (j in [0..<points.size()]) {
                    if (left(this.at(i - 1), this.at(i), this.at(j)) and rightOn(this.at(i - 1), this.at(i), this.at(j - 1))) { // if line intersects with an edge
                        var intersectionPoint2 is Point2 = getIntersection(this.at(i - 1), this.at(i), this.at(j), this.at(j - 1)); // find the point of intersection
                        if (right(this.at(i + 1), this.at(i), intersectionPoint2)) { // make sure it's inside the poly
                            var distance is Number = sqdist(this.at(i), intersectionPoint2);
                            if (distance < lowerDist) { // keep only the closest intersection
                                lowerDist = distance;
                                lowerIntersection = intersectionPoint2;
                                lowerIndex = j;
                            }
                        }
                    }
                    if (left(this.at(i + 1), this.at(i), this.at(j + 1)) and rightOn(this.at(i + 1), this.at(i), this.at(j))) {
                        var intersectionPoint2 = getIntersection(this.at(i + 1), this.at(i), this.at(j), this.at(j + 1));
                        if (left(this.at(i - 1), this.at(i), intersectionPoint2)) {
                            var distance = sqdist(this.at(i), intersectionPoint2);
                            if (distance < upperDist) {
                                upperDist = distance;
                                upperIntersection = intersectionPoint2;
                                upperIndex = j;
                            }
                        }
                    }
                }
                var lowerPoly is PolyDecompBayazit;
                var upperPoly is PolyDecompBayazit;

                // if there are no vertices to connect to, choose a point in the middle
                if (lowerIndex == (upperIndex + 1) mod points.size()) {
                    var steinerPoint is Point2 = new Point2(
                                                       (lowerIntersection.x + upperIntersection.x) * 0.5,
                                                       (lowerIntersection.y + upperIntersection.y) * 0.5);

                    lowerPoly = polyFromRange(i, upperIndex);
                    insert steinerPoint into lowerPoly.points;

                    if (i < upperIndex) {
                        upperPoly = polyFromRange(lowerIndex, i);
                    } else {
                        upperPoly = polyFromRange(0, i);
                    }
                    insert steinerPoint into upperPoly.points;
                } else {
                    // connect to the closest point within the triangle

                    // at(n) handles mod points.length, so increase upperIndex to make for loop easy
                    if (lowerIndex > upperIndex) upperIndex += points.size();

                    // Find closest point in range
                    var closestIndex is Integer = 0;
                    var closestDist is Float = Number.MAX_VALUE;
                    var closestVert is Point2 = null;

                    var j = lowerIndex;

                    while (j <= upperIndex) {
                        if (leftOn(this.at(i - 1), this.at(i), this.at(j)) and rightOn(this.at(i + 1), this.at(i), this.at(j))) {
                            var distance = sqdist(this.at(i), this.at(j));
                            if (distance < closestDist) {
                                closestDist = distance;
                                closestVert = this.at(j);
                                closestIndex = j mod points.size();
                            }
                        }

                        ++j;
                    }

                    lowerPoly = polyFromRange(i, closestIndex);
                    upperPoly = polyFromRange(closestIndex, i);
                }

                // solve smallest poly first
                if (lowerPoly.points.size() < upperPoly.points.size()) {
                    lowerPoly.decompose(cb);
                    upperPoly.decompose(cb);
                } else {
                    upperPoly.decompose(cb);
                    lowerPoly.decompose(cb);
                }
                return;
            }
        }

        if (points.size() >= 3) cb(this);
    }

    public function makeCCW to  () {
        var br is Integer = 0;

        // find bottom right point
        for(i in [1..<points.size()]) {
            if (this.at(i).y < this.at(br).y or (this.at(i).y == this.at(br).y and this.at(i).x > this.at(br).x)) {
                br = i;
            }
        }

        // reverse poly if clockwise
        if (not left(this.at(br - 1), this.at(br), this.at(br + 1))) {
            points = points.reverse();
        }
    }
}

                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                            